OPTION,  TFS=FALSE;
OPTION, ECHO=FALSE;
OPTION, INFO=FALSE;

TITLE, string="OPAL-ASTRA comparison";

USE_ET = -1;

IF (USE_ET > 0) {
 OPTION, AUTOPHASE=0;
 OPTION, PSDUMPFREQ=1000000;
 CTF3phi    = ( 118.609/180.0*PI);
 FINSB01phi = ( 112.186/180.0*PI);
 FINSB02phi = ( 111.257/180.0*PI);
}
else {
 OPTION, AUTOPHASE=4;
 OPTION, PSDUMPFREQ=1000000;
 CTF3phi    = ((-3.5+0.5)/180.0*PI);
 FINSB01phi = (0.0/180.0*PI);	
 FINSB02phi = (0.0/180.0*PI);
}

TWDS             = 0.05;       // shift of tw cavity for astra compatibility
QB               = 2e-10;
BFREQ            = 2998.0;
BCURRENT         = QB*BFREQ;
MINSTEPFORREBIN  = 1000;


CTF3: RFCAVITY, L = 0.34986, VOLT = 100., FMAPFN = "../GUN.dat",
		ELEMEDGE = 0.0, TYPE = "STANDING", FREQ = BFREQ, 
		LAG = CTF3phi;

FINLB01_MSL: SOLENOID, L = 0.6, KS = 0.2089, FMAPFN = "../SOL.dat", ELEMEDGE = 0.3;

FINSB01_RAC: TRAVELINGWAVE, L = 1.4, VOLT = 19.0, FMAPFN = "../TWS.dat",
			    ELEMEDGE = 2.95+TWDS, NUMCELLS = 121, MODE = 1/3, 
                            FREQ = BFREQ, LAG = FINSB01phi;

FINSB01_MSL10: SOLENOID, L = 0.6, KS = 0.08, FMAPFN = "../SOL.dat", ELEMEDGE = 3.3;
FINSB01_MSL20: SOLENOID, L = 0.6, KS = 0.08, FMAPFN = "../SOL.dat", ELEMEDGE = 4.3;
FINSB01_MSL30: SOLENOID, L = 0.6, KS = 0.12, FMAPFN = "../SOL.dat", ELEMEDGE = 5.3;
FINSB01_MSL40: SOLENOID, L = 0.6, KS = 0.12, FMAPFN = "../SOL.dat", ELEMEDGE = 6.3;

FINSB02_RAC: TRAVELINGWAVE, L = 1.4, VOLT = 22.0, FMAPFN = "../TWS.dat",
			    ELEMEDGE = 7.95+TWDS, NUMCELLS = 121, MODE = 1/3, 
                            FREQ = BFREQ, LAG = FINSB02phi;

FINSB02_MSL10: SOLENOID, L = 0.6, KS = 0.068, FMAPFN = "../SOL.dat", ELEMEDGE = 8.3;
FINSB02_MSL20: SOLENOID, L = 0.6, KS = 0.068, FMAPFN = "../SOL.dat", ELEMEDGE = 9.3;
FINSB02_MSL30: SOLENOID, L = 0.6, KS = 0.068, FMAPFN = "../SOL.dat", ELEMEDGE = 10.3;
FINSB02_MSL40: SOLENOID, L = 0.6, KS = 0.068, FMAPFN = "../SOL.dat", ELEMEDGE = 11.3;

l1: LINE = (CTF3,FINLB01_MSL,
	    FINSB01_RAC,FINSB01_MSL10,FINSB01_MSL20,FINSB01_MSL30,FINSB01_MSL40,
	    FINSB02_RAC,FINSB02_MSL10,FINSB02_MSL20,FINSB02_MSL30,FINSB02_MSL40);

Dist1:DISTRIBUTION, DISTRIBUTION = "ASTRAFLATTOPTH",
SIGMAX = 2*0.000275, SIGMAPX = 0.0, CORRX = 0.0,
SIGMAY = 2*0.000275, SIGMAPY = 0.0, CORRY = 0.0,
SIGMAT = 0.0, PT = 0.0, SIGMAPT = 0.0, CORRT = 0.0,
TRISE = 7e-13, TFALL = 7e-13, TPULSEFWHM = 9.9e-12,
EKIN = 0.63, NBIN = 20, DEBIN = 80;

Fs1:FIELDSOLVER, FSTYPE=None, MX=16, MY=16, MT=32, 
       PARFFTX=true, PARFFTY=true, PARFFTT=true,
       BCFFTX=open, BCFFTY=open, BCFFTT=open, 
       BBOXINCR=1, GREENSF=INTEGRATED;

if (USE_ET > 0) {
 beam1: BEAM, PARTICLE=ELECTRON, pc=P0, NSLICE=100, BFREQ=BFREQ, BCURRENT=BCURRENT, CHARGE=-1;	
 SELECT, LINE = l1;
 // Simulation of the gun with step size DTGUN
 TRACK, LINE=l1, BEAM=beam1, MAXSTEPS=1000000, DT=1.0e-13, ZSTOP=0.2;
  RUN, METHOD = "PARALLEL-SLICE", BEAM = beam1, FIELDSOLVER = Fs1, DISTRIBUTION = Dist1;
 ENDTRACK;

 // Simulation of the beam with step size DT 
 TRACK,line=l1, BEAM=beam1, MAXSTEPS=1000000, DT=1.0e-12, ZSTOP=13.0;
  RUN, METHOD = "PARALLEL-SLICE", BEAM=beam1, FIELDSOLVER=Fs1;
 ENDTRACK;
}
else {
 beam1: BEAM, PARTICLE=ELECTRON, pc=P0, NPART=200000, BFREQ=BFREQ, BCURRENT=BCURRENT, CHARGE=-1;	
 SELECT, LINE = l1;
 // Simulation of the gun with step size DTGUN
 TRACK, line=l1, BEAM=beam1, MAXSTEPS=1000000, DT=1.0e-13, ZSTOP=0.2;
  RUN, METHOD = "PARALLEL-T", BEAM = beam1, FIELDSOLVER = Fs1, DISTRIBUTION = Dist1;
 ENDTRACK;

 // Simulation of the beam with step size DT 
 TRACK,LINE=l1, BEAM=beam1, MAXSTEPS=1000000, DT=1.0e-12, ZSTOP=13.0;
  RUN, method = "PARALLEL-T", BEAM=beam1, FIELDSOLVER=Fs1;
 ENDTRACK;
}

QUIT;
