Option, ECHO=FALSE;
/////////////////////////////////////////////////////////////////////////////////////
// input file for single particle track for PSI Ring with 4 NEW copper cavities.   //
// the initial conditions are obtained from FIXPO EORBIT mode.                     //
// NOTE: ONLY WORKS SERIALLY ON SINGLE NODE.                                       //
/////////////////////////////////////////////////////////////////////////////////////

Option, PSDUMPFREQ=50000000;
Option, SPTDUMPFREQ=10;
Option, SPTDUMPFREQ=10;
Option, PSDUMPEACHTURN=true;
Option, PSDUMPLOCALFRAME=false;


Title,string="OPAL-CyclT for PSI 590MeV Ring";

Edes=.072;
gamma=(Edes+PMASS)/PMASS;
beta=sqrt(1-(1/gamma^2));
gambet=gamma*beta;
P0 = gamma*beta*PMASS;
brho = (PMASS*1.0e9*gambet) / CLIGHT;
value,{gamma,brho,Edes,beta,gambet};

phi01=324.0;
phi02=phi01+180.0;
phi04=phi01;
phi05=phi01+180.0;
phi03=150.0;

volt1st=0.73491;
volt3rd=volt1st*4.0*0.16;

r1=1853.90; 
r2=4480.73;
rft1=1845.45;
rft2=4477.24;

frequency=50.65;
frequency3=3.0*frequency;

ring: Cyclotron, TYPE="RING", CYHARMON=6, PHIINIT=90.0,  
PRINIT=0.0009, RINIT=2132.0, SYMMETRY=8.0, RFFREQ=frequency, 
FMAPFN="../s03av.nar";
//PRINIT=0.00076, RINIT=2132.0, SYMMETRY=8.0, RFFREQ=frequency, 

rf0: RFCavity, VOLT=volt1st, FMAPFN="../Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = r1, RMAX = r2, ANGLE=35.0,  PDIS = 416.0, 
GAPWIDTH = 300.0, PHI0=phi01; 

rf1: RFCavity, VOLT=volt1st, FMAPFN="../Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = r1, RMAX = r2, ANGLE=125.0, PDIS = 416.0, 
GAPWIDTH = 300.0, PHI0=phi02;

rf3: RFCavity, VOLT=volt1st, FMAPFN="../Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = r1, RMAX = r2, ANGLE=215.0, PDIS = 416.0, 
GAPWIDTH = 300.0, PHI0=phi04;

rf4: RFCavity, VOLT=volt1st, FMAPFN="../Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = r1, RMAX = r2, ANGLE=305.0, PDIS = 416.0, 
GAPWIDTH = 300.0, PHI0=phi05; 

rf2: RFCavity, VOLT=volt3rd, FMAPFN="../Cav3.dat", TYPE="SINGLEGAP", 
FREQ=frequency3, RMIN = rft1, RMAX = rft2, ANGLE=260.0, PDIS = 452.0, 
GAPWIDTH = 250.0, PHI0=phi03; 

l1:   Line = (ring,rf0,rf1,rf2,rf3,rf4);

Dist1:DISTRIBUTION, DISTRIBUTION=fromfile,FNAME="../PartDatabase.dat"; 

Fs1:FIELDSOLVER, FSTYPE=NONE, MX=32, MY=32, MT=32, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=false,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open;

beam1: BEAM, PARTICLE=PROTON, pc=P0, SPACECHARGE=false, NPART=1, BCURRENT=0.0, CHARGE=1.0, BFREQ= frequency;

Select, Line=l1;

track,line=l1, beam=beam1,MAXSTEPS=3600*214*2, STEPSPERTURN=3600*2;
 run, file = "track_output", turns = 1, method = "CYCLOTRON-T", beam=beam1, fieldsolver=Fs1, distribution=Dist1;
endtrack;
Stop;
