// test for accelerating equilibrium orbit of reference particle in PSI Ring cyclotron.
// the initial conditions are obtained from FIXPO EORBIT mode.
Option, TFS=FALSE;
Option, ECHO=FALSE;


Option, PSDUMPFREQ=10;
Option, SPTDUMPFREQ=1;

Title,string="OPAL-CyclT test";

Edes=.072;
gamma=(Edes+PMASS)/PMASS;
beta=sqrt(1-(1/gamma^2));
gambet=gamma*beta;
P0 = gamma*beta*PMASS;
brho = (PMASS*1.0e9*gambet) / CLIGHT;
value,{gamma,brho,Edes,beta,gambet};

phi01=139.4281;
phi02=phi01+180.0;
phi04=phi01;
phi05=phi01+180.0;
phi03=phi01+10.0;


frequency=50.650;
frequency3=3.0*frequency;

ring: Cyclotron, TYPE="RING", CYHARMON=6, PHIINIT=0.0,  
PRINIT=-0.000174, RINIT=2130.0, SYMMETRY=8.0, RFFREQ=frequency, 
FMAPFN="s03av.nar";

rf0: RFCavity, VOLT=0.73491, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=35.0,  PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi01; 

rf1: RFCavity, VOLT=0.73491, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=125.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi02;

rf2: RFCavity, VOLT=0.47046, FMAPFN="Cav3.dat", TYPE="SINGLEGAP", 
FREQ=frequency3,RMIN = 1900.0, RMAX = 4500.0, ANGLE=170.0, PDIS = 452.0, 
GAPWIDTH = 250.0, PHI0=phi03; 

rf3: RFCavity, VOLT=0.73491, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=215.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi04;

rf4: RFCavity, VOLT=0.73491, FMAPFN="Cav1.dat", TYPE="SINGLEGAP", 
FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=305.0, PDIS = 416.0, 
GAPWIDTH = 220.0, PHI0=phi05; 

l1:   Line = (ring,rf0,rf1,rf2,rf3,rf4);

Dist1:DISTRIBUTION, DISTRIBUTION=fromfile,FNAME="PartDatabase.dat"; 

Fs1:FIELDSOLVER, FSTYPE=NONE, MX=64, MY=64, MT=64, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=false,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open;

beam1: BEAM, PARTICLE=PROTON, pc=P0, SPACECHARGE=true, NPART=1e6, BCURRENT=3.0E-3, CHARGE=1.0, BFREQ= frequency;

Select, Line=l1;
#track,line=l1, beam=beam1,MAXSTEPS=255800, DT=1.0e-10;
track,line=l1, beam=beam1,MAXSTEPS=1300*5, DT=0.2e-10;
 run, file = "track_output", turns = 1, method = "CYCLOTRON-T", beam=beam1, fieldsolver=Fs1, distribution=Dist1;
endtrack;
Stop;
