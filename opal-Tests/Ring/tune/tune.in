// test for betatron oscillation frequency calculation for PSI Ring cyclotron.
// called by tuning.bash and work together with FIXPO EORBIT output data. 

// The require the distribution file two particles: first one is referenc particle,
// and the other is off center particle with a dx and dz. as following:
// 2
// 0.0         0.0           0.0           0.0          0.0           0.00
// 0.01         0.0           0.0           0.0          0.01           0.00
// When the totoal particle number equal to 2, SEO mode is triggered automatically,
// navertheless of the existance of the cavity and other components. 

Option, TFS=FALSE;
Option, ECHO=FALSE;
Option, PSDUMPFREQ=24500000;
Option, SPTDUMPFREQ = 50;
Option, PSDUMPEACHTURN=false;
Option, PSDUMPLOCALFRAME=false;

Title,string="OPAL-CyclT test";

CALL,FILE="data.dat";

//energy=233.997;
//r=3422.6;
//pr=81.492;

Edes= energy/1000.0; //GeV
r0  = r;         //mm
pr0 = pr/1000.0; //m_0*c

frequency=50.65;
frequency3=3.0*frequency;

gamma=(Edes+PMASS)/PMASS;
beta=sqrt(1-(1/gamma^2)); 
gambet=gamma*beta; 
P0 = gamma*beta*PMASS;
brho = (PMASS*1.0e9*gambet) / CLIGHT;
value,{gamma,brho,Edes,beta,gambet}; 

ring: Cyclotron, TYPE="RING", CYHARMON=6, PHIINIT=0.0, PRINIT=pr0, RINIT=r0 , SYMMETRY=8.0, RFFREQ=frequency, FMAPFN="../s03av.nar",
		 TCR1=4000, TCR2=4200, MBTC=0.05 ;
l1:   Line = (ring);
Select, Line=l1;

Dist1:DISTRIBUTION, DISTRIBUTION=fromfile,FNAME="PartDataTune.dat"; 

Fs1:FIELDSOLVER, FSTYPE=NONE, MX=64, MY=64, MT=64, 
		 PARFFTX=true, PARFFTY=true, PARFFTT=false,
		 BCFFTX=open, BCFFTY=open, BCFFTT=open;

beam1: BEAM, PARTICLE=PROTON, pc=P0, SPACECHARGE=false, NPART=1, BCURRENT=1.0E-6,CHARGE=1.0, BFREQ= frequency;

Select, Line=l1;

track,line=l1, beam=beam1,MAXSTEPS=720*100,STEPSPERTURN=720;
 run, method = "CYCLOTRON-T", beam=beam1, fieldsolver=Fs1, distribution=Dist1;
endtrack;
Stop;

