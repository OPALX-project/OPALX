Option, TFS=FALSE;
Option, ECHO=FALSE;
Option, PSDUMPFREQ=10000;
Option, STATDUMPFREQ=10000;
Option, INFO=TRUE;
Option, PPDEBUG=FALSE;

Title, string="Cyclotron_Multipacting_Simulation_CIAE_100MeV";

MAXPARTSNUM=10000000; // if boundary geometry is attached in a simulation upper limit of the number of simulation particles can be specified by MAXPARTSNUM, if this number get exceeded in simulation, Parallel-T tracker will stop.

// SECONDARYFLAG = 2: Using Vaughan's secondary emission model.
// Set NEMISSIONMODE=false will use constant simulation particle approach.
// Mean energy of emitted particles can be specified by the thermal velocity. Here we choose 1.5eV (i.e.,velocity of 7.268929821*1e5 m/s) 

DistSurf: DISTRIBUTION, DISTRIBUTION = "SURFACEEMISSION", NPDARKCUR =0,INWARDMARGIN = 0.0, FNBETA = 0.1, FNMAXEMI = 2, 
                                                          NEMISSIONMODE=false, SECONDARYFLAG = 2,  VSEYZERO=0.5, VEZERO=10.0, 
							  VSEYMAX=1.9, VEMAX=350.0, VKENERGY=1.0, VKTHETA=1.0, 
							  VVTHERMAL=7.268929821*1e5; // initial conditions

DistSurf1: DISTRIBUTION, DISTRIBUTION = "SURFACERANDCREATE",INWARDMARGIN = 0.0, NPDARKCUR =10000, EINITHR = 0.2,
							    VVTHERMAL=7.268929821*1e5; // mean energy of emission electrons 1.5eV

ge: GEOMETRY, FGEOM="../10.h5", S=0.0, ZSHIFT=0.631, DISTRS={DistSurf, DistSurf1};


value,{Phi};


// L:		physical element length (real in m)
// VOLT:	field scaling factor (real)
// ELEMEDGE:	physical start of the element on the floor (real in m)
// FREQ:	RF frequency of cavity (real in MHz).

Box: RFCavity, PLENGTH = 1.262, VOLT = 1, GEOMETRY = ge, FMAPFN = "../CyciaeEM.h5",
	       ELEMEDGE =0, FAST=true, FREQ =44.6, LAG =(1-1)*5/180*3.1415926, DX = 0, DY = 0, DZ = 0;

Benchmark: Line = (Box);

Mag: CYCLOTRONVALLEY, FMAPFN = "../CyciaeMagReal.h5", ELEMEDGE =0,DX = 0, DY = 0, DZ = 0;
Benchmark: Line = (Box, Mag);

// Begin: Fieldsolver ///////////////////////////////////////////
//
// Definition of first field solver.
//
Fs1:FIELDSOLVER, FSTYPE = NONE, MX = 32, MY = 32, MT = 128,
		 PARFFTX = true, PARFFTY = true, PARFFTT = false,
		 BCFFTX = open, BCFFTY = open, BCFFTT = open,
		 BBOXINCR = 0.1, GREENSF = INTEGRATED;
// End: Fieldsolver ////////////////////////////////////////////





qb=0.2e-9;
bfreq=300;
bcurrent=qb*bfreq;


beam1: BEAM, PARTICLE = ELECTRON, pc = P0, NPART = 2000, BFREQ = bfreq , BCURRENT = bcurrent, CHARGE =-1;

Select, Line=Benchmark;

track, line= Benchmark, beam=beam1, MAXSTEPS=23000, DT=4e-12, ZSTOP=30000;  
 run, method = "PARALLEL-T", beam = beam1, fieldsolver = Fs1, MULTIPACTING=true;
endtrack;


Quit;
